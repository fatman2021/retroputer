0 REM MODIFIED FROM https://sourceforge.net/p/buraphakit/MB_git/ci/HEAD/tree/CSCLASSICS/ERATOSTHENESE.BAS#l26
5 REM MODIFIED TO USE MEMORY INSTEAD OF ARRAYS
10 REM SIEVE OF ERATOSTHENES
15 REM MODIFED FROM QUITE BASIC MATH PROJECT DEMO
20 REM 
25 REM L IS THE LIMIT OF THE SIEVE
30 REM WE WILL FIND ALL PRIME NUMBERS UP TO L
35 ST = (IN(1) * 60 + IN(2))
40 L = 1000
45 REM N IS THE BASE POINTER TO THE SIEVE; B IS THE BANK
50 B = 6
60 REM FILL THE SIEVE WITH ALL NUMBERS UP TO L
70 I = 1
80 POKE B, I, 1
90 I = I + 1
95 IF I <= L THEN GOTO 80
97 PRINT
98 MD = (IN(1) * 60 + IN(2))
99 PRINT "TIME TAKEN: "; MD; "-"; ST; "="; MD - ST; "s"
100 REM START WITH THE FIRST PRIME NUMBER: 2
110 P = 2: C = 0
120 PRINT P,:C=C+1
130 REM "CROSS OUT" MULTIPLES OF P
140 I = P
150 POKE B, I, 0
160 I = I + P: IF I <= L THEN GOTO 150
170 REM FIND THE NEXT NUMBER NOT CROSSED OUT
180 P = P + 1
190 IF P = L THEN GOTO 220
194 V = PEEK(B,P)
200 IF V <> 0 THEN GOTO 120
210 GOTO 180
220 PRINT
230 EN = (IN(1) * 60 + IN(2))
240 PRINT "TIME TAKEN: "; EN; "-"; ST; "="; EN - ST; "s"
250 PRINT "PRIMES: "; C
